buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:3.1.0"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.5"
}

version "1.0.2-patched"
group "com.binxhealth"

apply plugin:"idea"
apply plugin:"org.grails.grails-plugin"
apply plugin:"org.grails.grails-plugin-publish"
apply plugin:"asset-pipeline"
apply plugin:"org.grails.grails-gsp"
apply plugin:"maven-publish"

ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
    repoName = rootProject.name
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

allprojects { proj ->
    tasks.withType(JavaCompile) {
        //I don't believe those warnings add value given modern IDEs
        options.warnings = false
        options.encoding = 'UTF-8'
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

jar {
    exclude "test/**"
    exclude "test/**/**"
}

sourcesJar {
    exclude "test/**"
    exclude "test/**/**"

    classifier = 'sources'
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-web-boot"
    compile "org.grails:grails-logging"
    compile "org.grails:grails-plugin-rest"
    compile "org.grails:grails-plugin-databinding"
    compile "org.grails:grails-plugin-i18n"
    compile "org.grails:grails-plugin-services"
    compile "org.grails:grails-plugin-url-mappings"
    compile "org.grails:grails-plugin-interceptors"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:async"
    compile "org.grails.plugins:scaffolding"
    compile "org.grails.plugins:gsp"
    compile "com.microsoft.graph:microsoft-graph:2.5.0"
    compile "com.microsoft.graph:microsoft-graph-auth:0.2.0"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web-plugin"
    provided "org.grails:grails-plugin-services"
    provided "org.grails:grails-plugin-domain-class"
    runtime "com.bertramlabs.plugins:asset-pipeline-grails:3.1.0"
    testCompile "org.grails:grails-gorm-testing-support"
    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails:grails-web-testing-support"

    // Spring Security
    compile 'org.grails.plugins:spring-security-core:3.2.1'
    compile 'org.grails.plugins:spring-security-oauth2:1.1.0'
    compile 'com.github.scribejava:scribejava-apis:6.3.0'
}

bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always')
    addResources = true
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}
// enable if you wish to package this plugin as a standalone application
bootRepackage.enabled = false

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

bintray {
    publications = ['maven']

    publish = true

    pkg {
        repo = 'maven'
        name = project.name
        websiteUrl = "https://github.com/cgoIT/$repoName"
        issueTrackerUrl = "https://github.com/cgoIT/$repoName/issues"
        vcsUrl = "https://github.com/cgoIT/${repoName}.git"
        licenses = ['Apache-2.0']
        labels = ['grails', 'plugin', 'grails-plugin', 'oauth2', 'azure', repoName]
        version {
            name = project.version
            attributes = ['grails-plugin': "$project.group:$project.name"]
        }
    }
}

grailsPublish {
    githubSlug = "cgoIT/$repoName"
    license {
        name = 'Apache-2.0'
    }
    title = "Grails Azure OAuth2 Plugin"
    desc = "Grails Azure OAuth2 Plugin"
    websiteUrl = "https://github.com/cgoIT/$repoName"
    issueTrackerUrl = "https://github.com/cgoIT/$repoName/issues"
    vcsUrl = "https://github.com/cgoIT/${repoName}.git"
    developers = [cgoIT:"Carsten GÃ¶tzinger"]
}

bintrayUpload.dependsOn = [sourcesJar]

assets {
    packagePlugin = true
}
